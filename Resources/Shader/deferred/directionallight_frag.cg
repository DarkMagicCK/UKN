const uniform float3 cameraPosition;
const uniform float4x4 inverseViewProj;

const uniform float2 gbufferSize;
const uniform float shadowMapSize;

// directional light properties
const uniform float3 lightDirection;
const uniform float4 lightColor;
const uniform float lightIntensity;
const uniform float lightPosition;

const uniform float depthPrecision;
const uniform float depthBias;
const uniform float4x4 lightViewProj;
const uniform int hasShadow;

struct VertexOutputType {
    float4 position: POSITION;
    float2 texCoord: TEXCOORD0;
};

// manual linear sampling
float4 manualSample(sampler2D sampler, float2 uv, float2 textureSize) {
    float2 texelPos = textureSize * uv;
    float2 lerps = frac(texelPos);
    float texelSize = 1.0 / textureSize;

    float4 sourcevals[4];
    sourcevals[0] = tex2D(sampler, uv);
    sourcevals[1] = tex2D(sampler, uv + float2(texelSize, 0));
    sourcevals[2] = tex2D(sampler, uv + float2(0, texelSize));
    sourcevals[3] = tex2D(sampler, uv + float2(texelSize, texelSize));

    float4 interpolated = lerp(lerp(sourcevals[0], sourcevals[1], lerps.x),
                               lerp(sourcevals[2], sourcevals[3], lerps.x),
                               lerps.y);
    return interpolated;
}

// phong shader
float4 phong(float3 position, float3 N, float specularIntensity, float specularPower) {
    float3 R = normalize(reflect(lightDirection, N));
    float3 E = normalize(cameraPosition - position.xyz);
    float NL = dot(N, -lightDirection);

    float3 diffuse = NL * lightColor.xyz;
    float specular = specularIntensity * pow(saturate(dot(R, E)), specularPower);

    return lightIntensity * float4(diffuse.rgb, specular);
}

float3 decode(float3 enc) {
    return (2.0f * enc.xyz - 1.0f);
}

float4 FragmentProgram(VertexOutputType input,
                       uniform sampler2D colorMap: TEXUNIT0,
                       uniform sampler2D normalMap: TEXUNIT1,
                       uniform sampler2D depthMap: TEXUNIT2,
                       uniform sampler2D shadowMap: TEXUNIT3
                       ): COLOR0 {

    float4 encodedNormal = tex2D(normalMap, input.texCoord);
    float3 normal = decode(encodedNormal.xyz);

    float specularIntensity = tex2D(colorMap, input.texCoord).a;
    float specularPower = encodedNormal.a * 255;

    float depth = manualSample(depthMap, input.texCoord, gbufferSize).r;
    float4 position = 1.0f;
    position.x = input.texCoord.x * 2.0f - 1.0f;
    position.y = -(input.texCoord.y * 2.0f - 1.0f);
    position.z = depth;
    position = mul(position, inverseViewProj);
    position /= position.w;

    float4 lightScreenPos = mul(position, lightViewProj);
    lightScreenPos /= lightScreenPos.w;

    float2 LUV = 0.5f * (float2(lightScreenPos.x, -lightScreenPos.y) + 1);
    float shadowFactor = 1.0f;

    if(hasShadow) {
        if(saturate(LUV.x) == LUV.x && saturate(LUV.y) == LUV.y) {
            float lZ = tex2D(shadowMap, LUV).r;
            float R = lightScreenPos.z;
            if(lZ < R - depthBias) {
                shadowFactor = 0.2f;
            }
        }
    }
    return shadowFactor * phong(position.xyz, normal, specularIntensity, specularPower);
    
}